.SHELL = bash

##
# Runs benchmarks.
# OPTION NIX --nix Use a Nix-built test executable instead of compiling with cabal in the user's shell.
benchmarks:
    set -eo pipefail

    if [ -n "$NIX" ]; then
        nix-build -A hpgsql-benchmarks -o local/hpgsql-benchmarks
        ./local/hpgsql-benchmarks/bin/hpgsql-benchmarks
    else
        cabal run -O2 hpgsql-benchmarks
    fi

##
# Runs all tests that CI runs, exactly like CI runs them.
ci-tests:
    set -eo pipefail

    # Postgres-version dependent tests for each possible version next
    # We test the last version with the vanilla nixpkgs-built derivation,
    # not the IOHK one. We assume differences in the codebase regarding different
    # postgres versions aren't enough to make it worth testing every version here too.
    concurrently -g --timings --names pg18,pg17,pg16,pg15,pg14 \
      'run tests --nix --pg 18' \
      'run tests --nix --pg 17' \
      'run tests --nix --pg 16' \
      'run tests --nix --pg 15' \
      'run tests --nix --pg 14'


##
# Formats all Haskell files with fourmolu.
format-hs:
    set -eo pipefail

    readarray -d '' FILES < <(git ls-files -z '*.hs')

    for f in "${FILES[@]}"; do
        if [ "$f" = "src/Codd/Internal.hs" ]; then
            echo "Skipping \"$f\" because of some fourmolu bug"
        else
            echo "Formatting $f..."
            fourmolu --mode inplace "$f"
        fi
    done

##
# Runs tests in an environment where there is a postgresql DB listening.
# OPTION PG --pg <pg> The version of the postgresql instance to start or 'all'. Defaults to 18 if unspecified.
# OPTION NIX --nix Run tests in a Nix derivation's sandbox
tests:
    set -eo pipefail
    [ -z "$PG" ] && PG=(18)
    [ "${PG[0]}" = "all" ] && PG=(14 15 16 17 18)
    [ -n "$1" ] && TARGS=$(printf "'%s' " "${@}")
    
    for pg in "${PG[@]}"; do
        echo "Running tests on Postgres $pg"
        if [ -n "$NIX" ]; then
            nix-build --no-out-link -A "testsPg${pg}" --argstr hspecArgs "$TARGS"
        else
            cabal build -O0 hpgsql-tests
            nix-shell -A "shellPg${pg}" ./default.nix --run "./scripts/run-tests-db-internal.sh $TARGS"
        fi
    done

## 
# Runs hpgsql-simple-compat's tests.
# OPTION PG --pg <pg> The version of the postgresql instance to start or 'all'. Defaults to 18 if unspecified.
test-compat:
    set -eo pipefail
    [ -z "$PG" ] && PG=(18)
    [ "${PG[0]}" = "all" ] && PG=(14 15 16 17 18)
    TARGS=() && [ -n "$1" ] && TARGS=$(printf "'%s' " "${@}")
    
    for pg in "${PG[@]}"; do
        echo "Running tests on Postgres $pg"
        cabal build -O0 hpgsql-simple-compat:test
        nix-shell -A "shellPg${pg}" ./default.nix --run "./scripts/run-hpgsql-simple-compat-tests-db-internal.sh $TARGS"
    done
